#── Core VPCs (private-only) ────────────────────────────────────────
resource "aws_vpc" "core" {
  for_each = var.vpc_configs

  cidr_block           = each.value.core.primary
  enable_dns_hostnames = true

  tags = merge(var.tags, {
    Name      = "${each.key}-core-vpc"
    Module    = "terraform-aws-core"
    ManagedBy = "terraform"
  })
}

resource "aws_vpc_ipv4_cidr_block_association" "core_secondary" {
  for_each = var.vpc_configs

  vpc_id     = aws_vpc.core[each.key].id
  cidr_block = each.value.core.secondary
}


#── Edge VPCs (public-facing) ─────────────────────────────────────
resource "aws_vpc" "edge" {
  for_each = var.vpc_configs

  cidr_block           = each.value.edge.primary
  enable_dns_hostnames = true

  tags = merge(var.tags, {
    Name      = "${each.key}-edge-vpc"
    Module    = "terraform-aws-core"
    ManagedBy = "terraform"
  })
}

resource "aws_vpc_ipv4_cidr_block_association" "edge_secondary" {
  for_each = var.vpc_configs

  vpc_id     = aws_vpc.edge[each.key].id
  cidr_block = each.value.edge.secondary
}

#─────────────────────────────
# Terraform outputs 
#─────────────────────────────
# Core VPC IDs
output "core_vpc_ids" {
  description = "Core VPC IDs keyed by VPC name"
  value = { for k, v in aws_vpc.core : k => v.id }
}

# Edge VPC IDs
output "edge_vpc_ids" {
  description = "Edge VPC IDs keyed by VPC name"
  value = { for k, v in aws_vpc.edge : k => v.id }
}